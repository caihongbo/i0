allprojects {
    group = 'com.thoughtworks.i0'
    version = '0.2.0-SNAPSHOT'
}

subprojects {
    repositories {
        mavenCentral()
        maven {
            url 'https://github.com/vincentx/i0/raw/master/repository'
        }
    }

    project.ext {
        jerseyVersion = '1.16'
        jettyVersion = '9.0.0.M3'
        jacksonVersion = '2.1.1'
        guavaVersion = '13.0.1'
        guiceVersion = '3.0'
        logbackVersion = '1.0.7'
        slf4jVersion = '1.7.2'
        flywayVersion = '2.0.2'
        hibernateJpaVersion = '4.1.7.Final'
        hibernateValidatorVersion = '4.3.0.Final'

        junitVersion = '4.10'
        mockitoVersion = '1.9.5-rc1'
    }

    apply plugin: 'java'
    apply plugin: 'maven'

    sourceCompatibility = '1.7'

    uploadArchives {
        repositories {
            mavenDeployer {
                repository(url: uri('../repository'))
            }
        }
    }

}

configure(subprojects.findAll { it.name != 'i0-gradle' }) {
    dependencies {
        compile 'com.google.code.findbugs:jsr305:2.0.1'
        compile 'javax.inject:javax.inject:1'
        compile 'javax.validation:validation-api:1.0.0.GA'

        compile "com.google.guava:guava:$guavaVersion"
        compile "com.google.inject:guice:$guiceVersion"

        compile "org.slf4j:slf4j-api:$slf4jVersion"

        runtime "org.hibernate:hibernate-validator:$hibernateValidatorVersion"

        testCompile "junit:junit:$junitVersion"
        testCompile "org.mockito:mockito-all:$mockitoVersion"
    }
}

project(':i0-core') {
    dependencies {
        compile "javax.servlet:servlet-api:3.0"
        compile "com.google.inject.extensions:guice-servlet:$guiceVersion"

        compile "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"
        compile "com.fasterxml.jackson.datatype:jackson-datatype-guava:$jacksonVersion"
        compile "com.fasterxml.jackson.module:jackson-module-jaxb-annotations:$jacksonVersion"
        compile "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:$jacksonVersion"

        compile "ch.qos.logback:logback-classic:$logbackVersion"
        compile "org.slf4j:jul-to-slf4j:$slf4jVersion"
        compile "org.slf4j:log4j-over-slf4j:$slf4jVersion"

        compile "com.sun.jersey:jersey-core:$jerseyVersion"

        testCompile "com.google.inject.extensions:guice-servlet:$guiceVersion"
        testCompile "org.eclipse.jetty:jetty-servlet:$jettyVersion"
        testCompile "org.eclipse.jetty:jetty-client:$jettyVersion"
    }
}

project(':i0-jetty') {
    dependencies {
        compile project(':i0-core')

        compile "com.google.inject.extensions:guice-servlet:$guiceVersion"
        compile "org.eclipse.jetty:jetty-servlet:$jettyVersion"
        compile "org.eclipse.jetty.websocket:websocket-servlet:$jettyVersion"
        compile "org.eclipse.jetty.websocket:websocket-server:$jettyVersion"

        testCompile "org.eclipse.jetty:jetty-client:$jettyVersion"
        testCompile "org.eclipse.jetty.websocket:websocket-client:$jettyVersion"
    }
}

project(':i0-jersey') {
    dependencies {
        compile project(':i0-core')

        compile "com.sun.jersey:jersey-core:$jerseyVersion"
        compile "com.sun.jersey:jersey-server:$jerseyVersion"
        compile "com.sun.jersey:jersey-servlet:$jerseyVersion"
        compile "com.sun.jersey.contribs:jersey-guice:$jerseyVersion"

        runtime "com.fasterxml.jackson.jaxrs:jackson-jaxrs-json-provider:$jacksonVersion"

        testCompile project(':i0-jetty')
        testCompile "com.sun.jersey:jersey-client:$jerseyVersion"
    }
}

project(':i0-persist-jpa') {
    dependencies {
        compile project(':i0-core')

        compile "com.google.inject.extensions:guice-persist:$guiceVersion"
        compile "com.googlecode.flyway:flyway-core:$flywayVersion"

        testCompile project(':i0-jetty')
        testCompile 'org.hibernate.javax.persistence:hibernate-jpa-2.0-api:1.0.1.Final'
        testCompile "org.eclipse.jetty:jetty-client:$jettyVersion"
        testRuntime "org.hibernate:hibernate-entitymanager:$hibernateJpaVersion"
        testRuntime 'com.h2database:h2:1.3.170'
    }
}

project(':i0-gradle') {
    apply plugin: 'groovy'

    dependencies {
        compile gradleApi()
        groovy localGroovy()
    }
}